{
  inputs = {
    nixpkgs.url = "https://channels.nixos.org/nixos-unstable/nixexprs.tar.xz";

    treefmt-nix.url = "github:numtide/treefmt-nix";
    treefmt-nix.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = {
    self,
    nixpkgs,
    treefmt-nix,
    ...
  }: let
    inherit (nixpkgs) lib;
    inherit (lib.attrsets) genAttrs mapAttrs' nameValuePair;

    systems = ["x86_64-linux" "aarch64-linux"];
    forAllSystems = genAttrs systems;
    nixpkgsFor = forAllSystems (system: nixpkgs.legacyPackages.${system});
    treefmtFor = forAllSystems (system: treefmt-nix.lib.evalModule nixpkgsFor.${system} ./treefmt.nix);
  in {
    packages = forAllSystems (_: {});

    devShells = forAllSystems (system: let
      pkgs = nixpkgsFor.${system};
    in {
      default = pkgs.mkShellNoCC {};
    });

    nixosConfigurations.vm = lib.nixosSystem {
      system = "x86_64-linux";
      modules = [
        ({modulesPath, ...}: {imports = [(modulesPath + "/virtualisation/qemu-vm.nix")];})
        ({pkgs, ...}: {
          virtualisation = {
            qemu.options = ["-nographic" "-serial" "mon:stdio"];
            cores = 4;
            memorySize = 4 * 1024;
            forwardPorts = [
              {
                from = "host";
                host.port = 8080;
                guest.port = 80;
              }
              {
                from = "host";
                host.port = 8443;
                guest.port = 443;
              }
            ];
          };
          system.stateVersion = lib.trivial.release;
          nix = {
            settings.trusted-users = ["root" "nixos"];
            extraOptions = "experimental-features = nix-command flakes";
          };
          programs.fish.enable = true;
          security.sudo.wheelNeedsPassword = false;
          users.users.nixos = {
            isNormalUser = true;
            hashedPassword = "";
            extraGroups = ["wheel"];
            shell = pkgs.fish;
          };
          services = {
            qemuGuest.enable = true;
            spice-vdagentd.enable = true;
          };
        })
      ];
    };

    formatter = forAllSystems (system: treefmtFor.${system}.config.build.wrapper);

    checks = forAllSystems (system: let
      packages = mapAttrs' (n: nameValuePair "package-${n}") self.packages.${system};
      devShells = mapAttrs' (n: nameValuePair "devShell-${n}") self.devShells.${system};
      formatting = {formatting = treefmtFor.${system}.config.build.check self;};
    in
      packages // devShells // formatting);
  };
}
